#INCLUDE "LibrBol.ch"
#INCLUDE "SIGAWIN.CH"        // incluido pelo assistente de conversao do AP5 IDE em 09/09/99
#INCLUDE "RWMAKE.CH"

// Define os valores fixos para a matriz aFatura
#define ID_BASIMP		1
#define ID_VALIMP		2
#define ID_SERIE		3
#define ID_NUMERO		4
#define ID_DTENTRADA	5
#define ID_DTCANC		6
#define ID_RUT			7
#define ID_NOME			8
#define ID_EXENTAS		9
#define ID_VALCONT		10
#define ID_ARRAY		10 // informa o total de elementos da matriz

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³LIBRBOL   ºAutor  ³Fernando Machima    º Data ³  25/04/01               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Livro Fiscal de Vendas para Boletas e Faturas (Loc. Chile)             º±±
±±º          ³                                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SigaLoja                                                   			  º±±
±±ºÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄº±±
±±º                         ATUALIZACOES SOFRIDAS                         			  º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄº±±
±±ºProgramador  ³ Data   ³ BOPS ³          Motivo da Alteracao           			  º±±
±±ºÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄº±±
±±ºFabio Fongaro³10.06.05³083053³- Considerar na tabela "SE" do SX5 as series que     º±±
±±º             ³        ³      ³comecam com "F" ou "N", como series de fatura.  	  º±±
±±º             ³        |      |O "N" representa nota de credito e debito.	 		  º±±
±±º             ³        |      |- Ajustar o indice do arquivo temporario             º±±
±±º             ³        |      |para: Tipo + Data de Entrada + Serie + Numero        º±±
±±º             ³        |      |- Informar no proprio arquivo de trabalho os campos  º±±
±±º             ³        |      |VALIMP e BASIMP que representam a soma de todos os   º±±
±±º             ³        |      |valores de impostos e base de impostos do documento  º±±
±±º             ³        |      |- Criar a matriz aFatura, pois conforme o Chile      º±±
±±º             ³        |      |deve existir apenas 1 linha por documento tipo Faturaº±±
±±º             ³        |      |e nao haver quebra por TES.                          º±±
±±ºÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function LibrBol()        // incluido pelo assistente de conversao do AP5 IDE em 09/09/99

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de variaveis utilizadas no programa atraves da funcao    ³
//³ SetPrvt, que criara somente as variaveis definidas pelo usuario,    ³
//³ identificando as variaveis publicas do sistema utilizadas no codigo ³
//³ Incluido pelo assistente de conversao do AP5 IDE                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cPerg     := "LIBBOL"

Private lFinRel	 := .F.
Private lSeries
Private aReturn  := {}
Private wnrel
Private m_pag    := 01
Private cString  := "SF3"
Private tamanho	 := "G"
Private limite 	 := 220
Private cDesc1   := ""
Private cDesc2   := ""
Private cDesc3   := ""
Private Titulo
PRIVATE nLastKey := 0
PRIVATE lContinua:= .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis utilizadas para parametros      ³
//³mv_par01             // Mes               ³
//³mv_par02             // Ano               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
titulo	 := PADC(OemToAnsi(STR0001),74) //"Emissao do Livro de Notas Fiscais e Boletas"
cDesc1   := PADC(OemToAnsi(STR0002),74) //"Serao solicitados o mes e ano de movimento para a emissao "
cDesc2 	 := PADC(OemToAnsi(STR0003),74) //"do livro de IVA Vendas"
cDesc3 	 := ""      
aReturn  := { OemToAnsi(STR0004), 1,OemToAnsi(STR0005), 1, 2, 1,"",1 } //"Especial"###"Administracion"
nLastKey := 0
lContinua:= .T.
wnrel    := "LIBRBOL"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica as perguntas selecionadas, busca o padrao da Nfiscal³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho)
If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)
If nLastKey == 27
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica Posicao do Formulario na Impressora³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
VerImp()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio do Processamento do Relatorio. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RptStatus({|| RptDetail()})              

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RptDetail ºAutor  ³Fernando Machima    º Data ³  05/05/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Executa o processamento de leitura e impressao dos         º±±
±±º          ³ registros do Livro de Vendas                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL		                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function RptDetail()

Local cBolIni   := ""  //Numero da boleta inicial. Usado para o intervalo por dia          
Local cBolAtu   := ""  //Numero da boleta final. Usado para o intervalo por dia          
Local cSerieF   := Space(TamSX3("F3_SERIE")[1])  //Serie para Fatura
Local lSerieF	:= .F.
Local cSerieB   := Space(TamSX3("F3_SERIE")[1])  //Serie para Boleta
Local lSerieB	:= .F.
Local cSerieE1  := Space(TamSX3("F3_SERIE")[1])  //Serie para Isentos(sem Boleta)
Local lSerieE1	:= .F.
Local cSerieE2  := Space(TamSX3("F3_SERIE")[1])  //Serie para Isentos(com Boleta)   
Local lSerieE2	:= .F.
Local nTamSerie := TamSX3("F3_SERIE")[1]
Local cPictZero := PesqPict("SF3","F3_VALCONT",8)
Local cPictRUT  := PesqPict("SA1","A1_CGC")
Local cRUT      := ""  //RUT do cliente
Local cNome     := ""  //Nome do cliente
Local cSerieSF3 := ""  //Serie do registro SF3
Local cArqIndxF3:= ""
Local cChave    := ""
Local cFiltro   := ""

Local aArea     := {}
Local aFatura   := {}   // Controla a impressao de faturas, agrupadas por numero do documento e nao por TES
Local aSeries   := {}   //Contem as series dos documentos gravadas na tabela SE do SX5
Local aIsento   := {}   //Contem a serie e numero dos documentos isentos. Usado para 
                        //identificar documentos que jah foram considerados no relatorio
Local aImpostos := {}    //Array com os impostos(base e valor) das faturas
Local aImpsBol  := {0,0} //Array com o total dos impostos(base e valor) das boletas
LocaL aTesImpInf  := {}  //Array com os impostos do TES 
Local nDiaIse1    := 0  //Total diario(F3_VALCONT) para os documentos Isentos(sem boletas)
Local nDiaIse2    := 0  //Total diario(F3_VALCONT) para os documentos Isentos(com boletas)
Local nDiaAfeto   := 0  //Total diario(F3_VALCONT) para os documentos afetos a IVA 
Local nTotalDia   := 0  //Total diario, considerando afetos e isentos
Local nPosIsento  := 0                       
Local nIndice     := 0 
Local nDif        := 0
Local nSigno

Local lPrim       := .T.  //Indica se trata da primeira serie de Isentos gravada no SX5
Local lImpBoleta  := .F.  //Indica se imprimiu boleta para o dia corrente

Local dDataAnt    := ctod("  /  /  ")
Local dEntAnt     
Local dDtIni      
Local lFitTop := .F.
Local cFilSer := ""
Local cFatura := "" // Controla a mudança do documento fiscal, no caso a fatura
Local cAlias  := "" // Controla o alias do arquivo temporario

Private nTotalBol  := 0  //Total geral do valor das boletas 	           
Private nTotalFat  := 0  //Total geral do valor das faturas
Private nTotalIse1 := 0  //Total geral do valor de isentos sem boleta      
Private nTotalIse2 := 0  //Total geral do valor de isentos com boleta              
Private nLin       := 0
Private nValImpFat := 0  //Valor total de impostos para Faturas, NCCs e NDCs
Private nBasImpFat := 0  //Base total de impostos para Faturas, NCCs e NDCs
Private nTotIsento := 0  //Total de isentos para Faturas
Private nPagina    := 0
Private cAliasSF3  :=""
Private aNota      := {}  //Contem os dados das NCCs e NDCs a serem impressas apos as faturas
Private nCount :=0
Private dDtFim

Private cPictVals   := PesqPict("SF3","F3_VALCONT")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Prepara o SF3 para extracao dos dados  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dDtIni := Ctod("01/"+StrZero(mv_par01,2)+"/"+mv_par02,"ddmmyyyy")
dDtFim := LastDay(dDtIni)

#IFDEF TOP    
	lFitTop := .T.
#ENDIF               

//Busca as series da fatura, da boleta e de isentos
dbSelectArea("SX5")  
DbSetOrder(1)
If dbSeek(xFilial("SX5")+"SE")
   While !Eof() .And. SX5->X5_FILIAL+SX5->X5_TABELA == xFilial("SX5")+"SE"
	  AAdd( aSeries,{SX5->X5_CHAVE,Substr(X5Descri(),1,1)})	  
	  //If Substr(X5Descri(),1,1) == "F"     // Faturas
	  If SubStr(X5Descri(),1,1) $ "FN" // Faturas, Notas de Credito e Notas de Debito
	     If !lSerieF
	     	cSerieF := Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	     	cSerieFTop := Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	     	lSerieF	:=.T.
	     Else
	     	cSerieF := cSerieF+"|"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	     	cSerieFTop :=cSerieFTop +"','"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	     EndIf	
	  ElseIf Substr(X5Descri(),1,1) == "B"     // Boletos
	     If !lSerieB
	     	cSerieB := Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))-nTamSerie))
	     	lSerieB:=.T.
	     Else
	     	cSerieB := cSerieB+"|"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	     EndIf
	  ElseIf Substr(X5Descri(),1,1) $ "I|E"   
	     //If Substr(X5Descri(),2,2) $ "SB"  // Serie  para Isentos(sem boleta)
	     IF "SB" $ X5Descri() // Procura a expressao SB, de sem boleta, no texto do tipo de serie
	        If !lSerieE1
	        	cSerieE1 := Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	        	lSerieE1:=.T.
	         Else
	        	cSerieE1 := cSerieE1+"|"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie))
	        EndIf	
	     Else     //Serie  para Isentos(com boleta)
	     	If !lSerieE2
	        	cSerieE2 := Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie ))
	        	lSerieE2:=.T.
	         Else
	        	cSerieE2 := cSerieE2+"|"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie ))
	        EndIf	
	     EndIf   
	  	 
	  EndIf   
      
     If Empty(cFilSer)
	  	cFilSer:=  Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie ))
	  Else 
	  	If lFitTop 
	  		cFilSer:=cFilSer+"','" + Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie ))
	  	Else
	  		cFilSer:=cFilSer+"|"+ Iif(Len(AllTrim(X5_CHAVE))== nTamSerie ,AllTrim(X5_CHAVE),AllTrim(X5_CHAVE)+Space(Len(AllTrim(X5_CHAVE))- nTamSerie ))
	  	EndIf
	  EndIf	
      
	  DbSkip()
   EndDo
EndIf   
                 
If lFitTop 
	cFilSer	:=	"('"+cFilSer+"')"  
	cSerieFTop :="('"+cSerieFTop+"')"  
EndIf
#IFDEF TOP
	
	If TcSrvType() != "AS/400"
	    cAliasSF3:= "TMP"
		lQuery	:=	.T.
		If Select("TMP") <> 0
			dbSelectArea("TMP")
			dbCloseArea()
		EndIf
		
        // Query para fazer a contagem para a regua de impressao
		cQuery := " SELECT COUNT(*) CONTA "
		cQuery += "	FROM "+	RetSqlName("SF3") + " SF3 "  // cQuery +="FROM SF3010  "
		cQuery += "   WHERE F3_FILIAL='"+xFilial("SF3")+"'"
		cQuery += "   AND  F3_ENTRADA BETWEEN '"+DTOS(dDtIni)+"' And '"+DTOS(dDtFim)+"'"		
		cQuery += "	  AND  F3_SERIE IN "+cFilSer
		cQuery += "	  AND  F3_TIPOMOV = 'V'"	
		cQuery += "   AND  D_E_L_E_T_ <> '*' "
		cQuery := ChangeQuery(cQuery)
		dbUsearea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TMP",.T.,.T.)
		dbSelectArea("TMP")
		nRecnos	:=	TMP->CONTA
		dbCloseArea()
        SetRegua(nRecnos)


		// Query do filtro       
		cQuery :="	SELECT  '1' TIPO ,F3_ENTRADA DTENTRADA ,F3_SERIE SERIE, F3_NFISCAL NUMERO,F3_CLIEFOR CLIENTE,F3_LOJA LOJA ,F3_ESPECIE ESPECIE,F3_VALCONT VALCONT,F3_TES TES,F3_EXENTAS EXENTAS, F3_DTCANC DTCANC,"
	    cQuery +="(F3_VALIMP1+F3_VALIMP2+F3_VALIMP3+F3_VALIMP4+F3_VALIMP5) VALIMP, (F3_BASIMP1+F3_BASIMP2+F3_BASIMP3+F3_BASIMP4+F3_BASIMP5) BASIMP,"
	
		cQuery += " R_E_C_N_O_ RECNO "
		cQuery += "	FROM "+	RetSqlName("SF3") + " SF3 "  // cQuery +="FROM SF3010  "
		cQuery += "   WHERE F3_FILIAL='"+xFilial("SF3")+"'"
		cQuery += "   AND  F3_ENTRADA BETWEEN '"+DTOS(dDtIni)+"' And '"+DTOS(dDtFim)+"'"		
		cQuery += "	  AND  F3_SERIE IN "+cFilSer
		cQuery += "	  AND  F3_TIPOMOV = 'V'"	
		cQuery += "   AND  D_E_L_E_T_ <> '*' "
		cQuery +="    AND  F3_SERIE IN " + cSerieFTop     
		cQuery +="union"

		cQuery +="	SELECT  '2' TIPO ,F3_ENTRADA DTENTRADA ,F3_SERIE SERIE, F3_NFISCAL NUMERO,F3_CLIEFOR CLIENTE,F3_LOJA LOJA ,F3_ESPECIE ESPECIE,F3_VALCONT VALCONT,F3_TES TES,F3_EXENTAS EXENTAS, F3_DTCANC DTCANC,"
	    cQuery +="(F3_VALIMP1+F3_VALIMP2+F3_VALIMP3+F3_VALIMP4+F3_VALIMP5) VALIMP, (F3_BASIMP1+F3_BASIMP2+F3_BASIMP3+F3_BASIMP4+F3_BASIMP5) BASIMP,"
		
		cQuery += " R_E_C_N_O_ RECNO "                                                                                                                                                                           
		cQuery += "	FROM "+	RetSqlName("SF3") + " SF3 " 
		cQuery += "   WHERE F3_FILIAL='"+xFilial("SF3")+"'"
		cQuery += "   AND  F3_ENTRADA BETWEEN '"+DTOS(dDtIni)+"' And '"+DTOS(dDtFim)+"'"		
		cQuery += "	  AND  F3_SERIE IN "+cFilSer
		cQuery += "	  AND  F3_TIPOMOV = 'V'"	
		cQuery += "   AND  D_E_L_E_T_ <> '*' "
		cQuery +="    AND  F3_SERIE NOT IN " + cSerieFTop     
		cQuery += "ORDER BY 1,2,3,4"
		cQuery := ChangeQuery(cQuery)
		dbUsearea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TMP",.T.,.T.)
		cAlias:="TMP"
		
		TCSetField("TMP","VALCONT","N",TamSX3("F3_VALCONT")[1],TamSX3("F3_VALCONT")[2])
		TCSetField("TMP","VALIMP" ,"N",TamSX3("F3_VALIMP1")[1],TamSX3("F3_VALIMP1")[2])
		TCSetField("TMP","BASIMP" ,"N",TamSX3("F3_BASIMP1")[1],TamSX3("F3_BASIMP1")[2])
	   	TCSetField("TMP","DTCANC","D",8,0)
	   	TCSetField("TMP","EXENTAS","N",TamSX3("F3_EXENTAS")[1],TamSX3("F3_EXENTAS")[2])
	   	TCSetField("TMP","DTENTRADA","D",8,0)
	 EndIf	 
  	
#ENDIF

If !lQuery   
	If Select("TRB") <> 0
		dbSelectArea("TRB")
		dbCloseArea()
	EndIf

    aStruct:={}
	Aadd(aStruct,{"TIPO"		,"C"	,1							,0})
	Aadd(aStruct,{"SERIE"		,"C"	,TamSX3("F3_SERIE")[1]		,TamSX3("F3_SERIE")[2]})
	Aadd(aStruct,{"NUMERO"		,"C"	,TamSX3("F3_NFISCAL")[1]	,TamSX3("F3_NFISCAL")[2]})
	Aadd(aStruct,{"CLIENTE"		,"C"	,TamSX3("F3_CLIEFOR")[1]	,TamSX3("F3_CLIEFOR")[2]})
	Aadd(aStruct,{"LOJA"		,"C"	,TamSX3("F3_LOJA")[1]		,TamSX3("F3_LOJA")[2]})
	Aadd(aStruct,{"ESPECIE"		,"C"	,TamSX3("F3_ESPECIE")[1]	,TamSX3("F3_ESPECIE")[2]})
	Aadd(aStruct,{"VALCONT"		,"N"	,TamSX3("F3_VALCONT")[1]	,TamSX3("F3_VALCONT")[2]})
	Aadd(aStruct,{"VALIMP" 		,"N"	,TamSX3("F3_VALIMP1")[1]	,TamSX3("F3_VALIMP1")[2]})
	Aadd(aStruct,{"BASIMP" 		,"N"	,TamSX3("F3_BASIMP1")[1]	,TamSX3("F3_BASIMP1")[2]})
	Aadd(aStruct,{"TES"			,"C"	,TamSX3("F3_TES")[1]		,TamSX3("F3_TES")[2]})
	Aadd(aStruct,{"DTENTRADA"	,"D"	,TamSX3("F3_ENTRADA")[1]	,TamSX3("F3_ENTRADA")[2]})
	Aadd(aStruct,{"EXENTAS"		,"N"	,TamSX3("F3_EXENTAS")[1]	,TamSX3("F3_EXENTAS")[2]})
	Aadd(aStruct,{"DTCANC"		,"D"	,TamSX3("F3_DTCANC")[1]		,TamSX3("F3_DTCANC")[2]})
	cArqTrab:=Criatrab(aStruct,.T.)
	DbUseArea(.T.,,cArqTrab,"TRB",.F.,.F.)
	IndRegua("TRB",cArqTRab,"TIPO + Dtos(DTENTRADA)+SERIE+NUMERO")
    
    cAliasSF3:= "SF3"
    cAlias   := "TRB"
    
	dbSelectArea(cAliasSF3)
	cFiltro :="F3_FILIAL=='"+xFilial("SF3")+"'.AND.DTOS(F3_ENTRADA)>='"+DTOS(dDtIni)+"'.AND. DTOS(F3_ENTRADA)<='"+DTOS(dDtFim)+"'"
	//cFiltro := cFiltro + ".And. ( AllTrim(SF3->F3_SERIE) $ '"+AllTrim(cSerieF)+"'  .or. AllTrim(SF3->F3_SERIE) $ '"+AllTrim(cSerieB)+"' .or. AllTrim(SF3->F3_SERIE) $ '"+AllTrim(cSerieE1)+"' .or. AllTrim(SF3->F3_SERIE) $ '"+AllTrim(cSerieE2)+"')"
	cFiltro := cFiltro + ".And. ( SF3->F3_SERIE $ '"+cFilSer +"')"
	cFiltro := cFiltro + ".And.F3_TIPOMOV == 'V'" 
	
	cChave:="F3_FILIAL+F3_SERIE+DTOS(F3_ENTRADA)+F3_ESPECIE"
	cArqIndxF3:=CriaTrab(NIL,.F.)
	
	IndRegua("SF3",cArqIndxF3,cChave,,cFiltro,OemToAnsi(STR0006)) //"Filtrando registros..."
	
	nIndice	:=	Retindex("SF3")	 
	#IFNDEF TOP
		dbSetIndex(cArqIndxF3+OrdBagExt())	
		DbSetOrder(nIndice+1)
	#ENDIF

	dbGoTop()
	
	dbSelectArea("TRB")
	While (cAliasSF3)->(!Eof())
	 	If  AllTrim((cAliasSF3)->F3_SERIE) $ AllTrim(cSerieF) 
		  	cTipo:= "1"
	   	Else
			cTipo:= "2"
	    EndIf
	    
		Reclock("TRB",.T.)     
		Replace TIPO		with cTipo
		Replace SERIE 		with (cAliasSF3)->F3_SERIE	 
		Replace NUMERO 		with (cAliasSF3)->F3_NFISCAL
		Replace CLIENTE		with (cAliasSF3)->F3_CLIEFOR
		Replace	LOJA		with (cAliasSF3)->F3_LOJA
		Replace	ESPECIE		with (cAliasSF3)->F3_ESPECIE
		Replace VALCONT		with (cAliasSF3)->F3_VALCONT
		Replace VALIMP 		with (cAliasSF3)->(F3_VALIMP1+F3_VALIMP2+F3_VALIMP3+F3_VALIMP4+F3_VALIMP5)
		Replace BASIMP 		with (cAliasSF3)->(F3_BASIMP1+F3_BASIMP2+F3_BASIMP3+F3_BASIMP4+F3_BASIMP5)
		Replace TES 		with (cAliasSF3)->F3_TES
		Replace DTENTRADA 	with (cAliasSF3)->F3_ENTRADA  	  
		Replace EXENTAS		with (cAliasSF3)->F3_EXENTAS 
		Replace DTCANC  	with (cAliasSF3)->F3_DTCANC
		
		(cAliasSF3)->(DbSkip())
	EndDo
	
	MsUnlock()
	cAlias:="TRB"
	SetRegua((cAlias)->(Reccount()))
EndIf

DbGoTop()
                   
R020CabF()    

If lAbortPrint
   @ 00,01 PSAY OemToAnsi(STR0007) //"** CANCELADO PELO OPERADOR **"
   lContinua := .F.
   Return
Endif
    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Impressao de vendas com faturas            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lFinRel := .T.          


dbSelectArea(cAlias)
DbGotop()

While !Eof() 

	cFatura := (cAlias)->(TIPO+DTOS(DTENTRADA)+SERIE+NUMERO)
	aFatura := ARRAY(ID_ARRAY)
	
	aFatura[ID_EXENTAS] := 0
	aFatura[ID_VALIMP]  := 0
	aFatura[ID_BASIMP]  := 0
	aFatura[ID_VALCONT] := 0

	While (cAlias)->TIPO =="1" .And. !Eof()
		IncRegua()
		
		cNome := Posicione("SA1",1,xFilial("SA1")+(cAlias)->(CLIENTE+LOJA),"A1_NOME")
		cRUT  := Posicione("SA1",1,xFilial("SA1")+(cAlias)->(CLIENTE+LOJA),"A1_CGC")
		cNome := LEFT(cNome,25)
		
		If AllTrim((cAlias)->ESPECIE) $ "NCC" // valor que diminue
			nSigno := -1
		Else
			nSigno := 1
		EndIf
		
		aFatura[ID_SERIE]		:= (cAlias)->SERIE
		aFatura[ID_NUMERO] 		:= (cAlias)->NUMERO
		aFatura[ID_DTENTRADA]	:= (cAlias)->DTENTRADA
		aFatura[ID_DTCANC]		:= (cAlias)->DTCANC
		aFatura[ID_RUT]			:= cRut
		aFatura[ID_NOME]		:= cNome
		aFatura[ID_EXENTAS]		+= (cAlias)->EXENTAS * nSigno
		aFatura[ID_VALIMP]		+= (cAlias)->VALIMP  * nSigno
		aFatura[ID_BASIMP]		+= (cAlias)->BASIMP  * nSigno
		aFatura[ID_VALCONT]		+= (cAlias)->VALCONT * nSigno
	   	DbSkip()
		           
		// Imprime quando mudar o numero da fatura
		IF cFatura <> (cAlias)->(TIPO+DTOS(DTENTRADA)+SERIE+NUMERO)
			@ nLin, 001 PSAY aFatura[ID_SERIE]
			@ nLin, 007 PSAY aFatura[ID_NUMERO]
			@ nLin, 018 PSAY aFatura[ID_DTENTRADA]
			
			IF EMPTY(aFatura[ID_DTCANC])
				@ nLin, 030 PSAY aFatura[ID_RUT]		PICTURE cPictRut
				@ nLin, 045 PSAY aFatura[ID_NOME]
				@ nLin, 082 PSAY aFatura[ID_EXENTAS]	PICTURE cPictVals
				@ nLin, 104 PSAY aFatura[ID_BASIMP]		PICTURE cPictVals
				@ nLin, 120 PSAY aFatura[ID_VALIMP]		PICTURE cPictVals
				@ nLin, 136 PSAY aFatura[ID_VALCONT]	PICTURE cPictVals
	                                                  
	      		nTotIsento += aFatura[ID_EXENTAS]
	      		nBasImpFat += aFatura[ID_BASIMP]
	      		nValImpFat += aFatura[ID_VALIMP]
	      		nTotalFat  += aFatura[ID_VALCONT]
		   	Else
		   		@ nLin,025 PSAY OemToAnsi(STR0008)  //"** A N U L A D A **"         
		   	EndIf
			nLin++
			
			cFatura := (cAlias)->(TIPO+DTOS(DTENTRADA)+SERIE+NUMERO)
			aFatura := ARRAY(ID_ARRAY)
	
			aFatura[ID_EXENTAS] := 0
			aFatura[ID_VALIMP]  := 0
			aFatura[ID_BASIMP]  := 0
			aFatura[ID_VALCONT] := 0
		EndIf
			
	   	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	   	//³Dispara a funcao para impressao do Rodape. ³
	   	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	   	If nLin > 55
	    	R020RodF()
	   	EndIf	  
    Enddo    
	R020RdpF()    


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Impressao de vendas com boletas e isentas  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R020CabB()    
	dEntAnt := (cAlias)->DTENTRADA	      	        
	While (cAlias)->TIPO == "2" .AND. !EOF()
	   		
	   		nDiaIse1    := 0
	   		nDiaIse2    := 0
	   		nDiaAfeto   := 0
	   		nTotalDia   := 0
	   		lImpBoleta  := .F.
	                                      
	   		If !Empty((cAlias)->DTCANC)
	      		DbSkip()
		  		Loop
	   		EndIf
	   
	   //Se satisfizer esta condicao, nao existem boletas para o dia corrente
   		If ((cAlias)->EXENTAS > 0) .And. (AllTrim((cAlias)->SERIE) $ AllTrim(cSerieE1) .Or. AllTrim((cAlias)->SERIE) $ AllTrim(cSerieE2))
	      //Se o documento isento estiver no array aIsento jah foi considerado no relatorio
      		If nPosIsento  := Ascan(aIsento,{|x| x[1] == (cAlias)->SERIE .And. x[2] == (cAlias)->NUMERO}) > 0      
	        	DbSkip()                                                                                 
         		dEntAnt  := (cAlias)->DTENTRADA
	         	Loop
	      	EndIf      
	   	EndIf
	   
   	   	cBolIni := (cAlias)->NUMERO        
	   	cBolAtu := (cAlias)->NUMERO       
	   	@ nLin,002 PSAY Substr(DTOC((cAlias)->DTENTRADA),1,2)      //Mostrar o dia
	   
	       
   		While !Eof() .And. AllTrim((cAlias)->SERIE) $ AllTrim(cSerieB).And. DTOS(dEntAnt) == DTOS((cAlias)->DTENTRADA)
		     IncRegua()
		     	      
		     If !Empty((cAlias)->DTCANC)
		        DbSkip()
		        Loop
		     EndIf
		     
	         lImpBoleta := .T.
		     cBolIni  := IIf((cAlias)->NUMERO < cBolIni,(cAlias)->NUMERO,cBolIni)
             cBolAtu  := IIf((cAlias)->NUMERO > cBolAtu,(cAlias)->NUMERO,cBolAtu)
             
		     nDiaAfeto           += (cAlias)->VALCONT	  //Total do valor das boletas do dia corrente
		     nTotalBol           += (cAlias)->VALCONT    
		     aImpsBol[ID_BASIMP] += (cAlias)->BASIMP
		     aImpsBol[ID_VALIMP] += (cAlias)->VALIMP
					
		     dbSelectArea((cAlias))
		     dbSkip()
		EndDO
			
	    While !Eof() .And. (AllTrim((cAlias)->SERIE) $ AllTrim(cSerieE2)) .And. DTOS(dEntAnt) == DTOS((cAlias)->DTENTRADA)                	   	  
	       	IncRegua()
	       	If Empty((cAlias)->DTCANC)                  	  
		 		nDiaIse2  += (cAlias)->EXENTAS  //Total do valor de isentos com boleta do dia corrente               
		       	Aadd(aIsento,{(cAlias)->SERIE,(cAlias)->NUMERO})	                 
		              	cBolIni  := IIf((cAlias)->NUMERO < cBolIni,(cAlias)->NUMERO,cBolIni)
		                cBolAtu  := IIf((cAlias)->NUMERO > cBolAtu,(cAlias)->NUMERO,cBolAtu)
		    EndIf   
	 	    DbSkip()
	    EndDo	   	     
	    
	    While !Eof() .And. (AllTrim((cAlias)->SERIE) $ AllTrim(cSerieE1)) .And. DTOS(dEntAnt) == DTOS((cAlias)->DTENTRADA)
	    	IncRegua()   
	        If Empty((cAlias)->DTCANC)                  	  
		       nDiaIse1  += (cAlias)->EXENTAS   //Total do valor de isentos sem boleta do dia corrente
		       Aadd(aIsento,{(cAlias)->SERIE,(cAlias)->NUMERO})
		       cBolIni  := IIf((cAlias)->NUMERO < cBolIni,(cAlias)->NUMERO,cBolIni)
		       cBolAtu  := IIf((cAlias)->NUMERO > cBolAtu,(cAlias)->NUMERO,cBolAtu)
		     EndIf   
	 	     DbSkip() 	               
	    EndDo
	    
	    /*
	 	nTotalIse1 += nDiaIse1	               	   	     
	 	nTotalIse2 += nDiaIse2	               	         
	 	nTotalDia := nDiaIse1 + nDiaIse2 + nDiaAfeto			 		   	   	      	        
	      */
	   If Empty((cAlias)->SERIE)
	      DbSkip()
	   Endif	     
	   
	   If (cAlias)->DTENTRADA != dEntAnt .Or. Eof()
	     	nTotalIse1 += nDiaIse1	               	   	     
		 	nTotalIse2 += nDiaIse2	               	         
		 	nTotalDia := nDiaIse1 + nDiaIse2 + nDiaAfeto			 		   	   	      	        
	        @ nLin,013 PSAY cBolIni        //Impressao do numero da boleta inicial para o dia corrente
	        @ nLin,025 PSAY cBolAtu                          //Numero do ultimo documento emitido no dia corrente
	        @ nLin,038 PSAY nDiaIse2		Picture cPictVals //Total de isentos com Boleta	
	        @ nLin,056 PSAY nDiaIse1		Picture cPictVals //Total de isentos sem Boleta		         
	        @ nLin,082 PSAY nDiaAfeto       Picture cPictVals //Total de vendas afetas a IVA	
	        @ nLin,105 PSAY 0               Picture cPictZero
	        @ nLin,112 PSAY nTotalDia		Picture cPictVals
		       
		    nLin := nLin + 1          
		    dEntAnt := (cAlias)->DTENTRADA	     
	
		    If nLin > 55
			   R020RodB()
		     EndIf	      	         
	   EndIf	     
	EndDo   
	lFinRel := .T.
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Dispara a funcao para impressao do Rodape.   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R020RodB()
	
	R020RdpB(aImpsBol)
		
EndDo

If Select(cAlias) <> 0
	DbSelectArea(cAlias)
	DbCloseArea()
EndIf	

#IFNDEF TOP
	SF3->(DbsetOrder(1)  )
	cArqTrab+=OrdBagExt()
	If FILE(cArqTrab)
		Ferase(cArqTrab)
	EndIf	
#ENDIF

Set Device To Screen
If aReturn[5] == 1
	Set Printer TO
	dbcommitAll()
	ourspool(wnrel)
Endif
MS_FLUSH()

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020CabB  ºAutor  ³Jose Lucas          º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Cabecalho do Libro de Boletas de IVA ( Ventas ).           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020Cab
Static Function R020CabB()

Local cRUT      := ""
Local cEmpresa  := ""
Local cTitulo   := ""
Local _nMes
Local aMeses    := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis utilizadas no cabecalho³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aMeses,OemToAnsi(STR0009)) //"ENERO    "
AADD(aMeses,OemToAnsi(STR0010)) //"FEBRERO  "
AADD(aMeses,OemToAnsi(STR0011)) //"MARZO    "
AADD(aMeses,OemToAnsi(STR0012)) //"ABRIL    "
AADD(aMeses,OemToAnsi(STR0013)) //"MAYO     "
AADD(aMeses,OemToAnsi(STR0014)) //"JUNIO    "
AADD(aMeses,OemToAnsi(STR0015)) //"JULIO    "
AADD(aMeses,OemToAnsi(STR0016)) //"AGOSTO   "
AADD(aMeses,OemToAnsi(STR0017)) //"SETIEMBRE"
AADD(aMeses,OemToAnsi(STR0018)) //"OCTUBRE  "
AADD(aMeses,OemToAnsi(STR0019)) //"NOVIEMBRE"
AADD(aMeses,OemToAnsi(STR0020)) //"DICIEMBRE"

cEmpresa	:= SM0->M0_NOMECOM
cRUT	    := TRANSFORM(SM0->M0_CGC,PesqPict("SA1","A1_CGC"))
cTitulo     := OemToAnsi(STR0021) //"V E N T A S   C O N   B O L E T A S"

nPagina := nPagina + 1
@ 02,000 PSAY OemToAnsi(STR0022) //"Empresa: "
@ 02,020 PSAY cEmpresa
@ 02,095 PSAY OemToAnsi(STR0023) //"Pagina Nro.: "
@ 02,110 PSAY StrZero(nPagina,6)

@ 03,000 PSAY OemToAnsi(STR0024) //"RUT: "
@ 03,007 PSAY cRUT

@ 05,000 PSAY cTitulo

_nMes := Month(dDtFim)

If _nMes > 0 .And. _nMes < 13
	@ 05,080 PSAY OemToAnsi(STR0025) //"Mes "
	@ 05,085 PSAY aMeses[_nMes]
	@ 05,095 PSAY OemToAnsi(STR0026) //"Ano"
	@ 05,100 PSAY StrZero(Year(dDtFim),4)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cabecalho para o Relatorio.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 07,000 PSAY OemToAnsi(STR0034) //"   Fecha         Rango de Boletas                   Ventas Exentas                                   Ventas Afectas IVA               "
@ 08,000 PSAY OemToAnsi(STR0035) //"                 Del n.   Al n.               Con Boletas     Sin Boletas                 Con Boletas    Sin Boletas    T O T A L     "   
nLin := 09
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020RodB  ºAutor  ³Jose Lucas          º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rodape do Libro de Boletas de IVA ( Ventas ).              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020Rod
Static Function R020RodB()

Local nTot := nTotalIse1 + nTotalIse2 + nTotalBol
//Local cPictZero := PesqPict("SF3","F3_VALCONT",6)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Dispara a funcao para impressao do Rodape.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nLin := nLin + 2
@ nLin,015 PSAY OemToAnsi(STR0039)   //"T O T A L E S : "         
@ nLin,038 PSAY nTotalIse2		Picture cPictVals	//Total de isentos com boleta
@ nLin,056 PSAY nTotalIse1		Picture cPictVals	//Total de isentos sem boleta	         
@ nLin,082 PSAY nTotalBol       Picture cPictVals	
//@ nLin,105 PSAY 0               Picture cPictZero
@ nLin,112 PSAY nTot    		Picture cPictVals

If  !(lFinRel)
	R020CabB()
	@ nLin,000 PSAY OemToAnsi(STR0028) //"Transporte :"
	nLin:= nlin+1
EndIf
lFinRel := .F.

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020RdpB  ºAutor  ³Jose Lucas		     º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rodape do Libro de Boletas de IVA ( Ventas ).              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL		                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020RdpB
Static Function R020RdpB(aImpsBol)

Local nValLiq    := 0
Local nTot       := nTotalFat + nTotalBol + nTotalIse1 + nTotalIse2
Local cLinRodaS1 := ""
Local cLinRodaS2 := ""
Local cLinRodaS3 := ""
Local cLinRoda1  := ""
Local cLinRoda2  := ""
Local cLinRoda3  := ""
Local cLinRoda3a := ""

nLin	:=	nLin + 1

nDecs1     := MsDecimais(1)

nValLiq    := aImpsBol[ID_BASIMP]
nValImpBol := aImpsBol[ID_VALIMP]

cLinRodaS1  := STR0040 + Space(10) + Trans(nValLiq,cPictVals)    //"SubTotais "
cLinRodaS2  := STR0041 + Space(10) + Trans(nValImpBol,cPictVals) //"Menos IVA "
cLinRodaS3  := STR0042 + Space(10) + Trans(nTotalBol,cPictVals)  //"Totais    "	   	   

cLinRoda1  := OemToAnsi(STR0029) + Trans(nBasImpFat,cPictVals) + Trans(nValImpFat,cPictVals) + Trans(nTotalFat,cPictVals)  //"Facturas           :" 

cLinRoda2  := OemToAnsi(STR0030) + Trans(nValLiq,cPictVals) + Trans(nValImpBol,cPictVals) + Trans(nTotalBol,cPictVals)  //"Boletas            :"

cLinRoda3  := OemToAnsi(STR0031)  //"Ventas Exentas IVA :"
cLinRoda3a := Trans(nTotalIse2 + nTotalIse1,cPictVals)

If nLin > 55
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Dispara a funcao para impressao do Cabecalho.     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R020CabB()
EndIf
nLin := nLin + 2
@ nLin,062 PSAY cLinRodaS1
nLin := nLin + 1
@ nLin,062 PSAY cLinRodaS2
nLin := nLin + 1
@ nLin,062 PSAY cLinRodaS3

nLin := nLin + 2
@ nLin,040 PSAY OemToAnsi(STR0027) //"Resumen :"
nLin := nLin + 1
@ nLin,062 PSAY cLinRoda1
nLin := nLin + 1
@ nLin,062 PSAY cLinRoda2
nLin := nLin + 1
@ nLin,062 PSAY cLinRoda3 
@ nLin,110 PSAY cLinRoda3a
nLin := nLin + 2
@ nLin,062 PSAY OemToAnsi(STR0039)   //"T O T A L E S : "         
@ nLin,096 PSAY Trans(nValImpFat + nValImpBol,cPictVals)
@ nLin,109 PSAY Trans(nTot,cPictVals)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ VerImp   ºAutor  ³Marcos Simidu       º Data ³  20/12/95   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica posicionamento de papel na Impressora             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicio da Funcao   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function VerImp
Static Function VerImp()

Local aDriver  :=  ""
Local nOpc     

nLin:= 0                // Contador de Linhas
aDriver:=ReadDriver()
If aReturn[5]==2
	
	nOpc       := 1
	While .T.
		
		SetPrc(0,0)
		dbCommitAll()
		
		@ 00   ,000	PSAY aDriver[5]
		@ nLin ,000 PSAY " "
		@ nLin ,004 PSAY "*"
		@ nLin ,022 PSAY "."
		
		IF MsgYesNo(OemToAnsi(STR0032)) //"¿Fomulario esta en posicion? "
		   nOpc := 1
		ElseIF MsgYesNo(OemToAnsi(STR0033)) //"¿Intenta Nuevamente? "
		   nOpc := 2
		Else
		   nOpc := 3
		Endif

		Do Case
		Case nOpc==1
			lContinua := .T.
			Exit
		Case nOpc==2
			Loop
		Case nOpc==3
			lContinua := .F.
			Return
		EndCase
	End
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020CabF  ºAutor  ³Jose Lucas          º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Cabecalho do Libro de Faturas de IVA ( Ventas ).           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020CabF
Static Function R020CabF()

Local cRUT      := ""
Local cEmpresa  := ""
Local cTitulo   := ""
Local _nMes
Local aMeses    := {}

AADD(aMeses,OemToAnsi(STR0009)) //"ENERO    "
AADD(aMeses,OemToAnsi(STR0010)) //"FEBRERO  "
AADD(aMeses,OemToAnsi(STR0011)) //"MARZO    "
AADD(aMeses,OemToAnsi(STR0012)) //"ABRIL    "
AADD(aMeses,OemToAnsi(STR0013)) //"MAYO     "
AADD(aMeses,OemToAnsi(STR0014)) //"JUNIO    "
AADD(aMeses,OemToAnsi(STR0015)) //"JULIO    "
AADD(aMeses,OemToAnsi(STR0016)) //"AGOSTO   "
AADD(aMeses,OemToAnsi(STR0017)) //"SEPTIEMBRE"
AADD(aMeses,OemToAnsi(STR0018)) //"OCTUBRE  "
AADD(aMeses,OemToAnsi(STR0029)) //"NOVIEMBRE"
AADD(aMeses,OemToAnsi(STR0020)) //"DICIEMBRE"

cEmpresa := SM0->M0_NOMECOM
cRUT	:= TRANSFORM(SM0->M0_CGC,PesqPict("SA1","A1_CGC"))
cTitulo	:= OemToAnsi(STR0036) //"V E N T A S   C O N   F A C T U R A S"   

nPagina := nPagina + 1
@ 02,000 PSAY OemToAnsi(STR0022) //"Empresa: "
@ 02,020 PSAY cEmpresa
@ 02,178 PSAY OemToAnsi(STR0023) //"Pagina Nro.: "
@ 02,194 PSAY StrZero(nPagina,6)

@ 03,000 PSAY OemToAnsi(STR0024) //"RUT: "
@ 03,007 PSAY cRUT

@ 05,000 PSAY cTitulo

_nMes := Month(dDtFim)

If _nMes > 0 .And. _nMes < 13
	@ 05,080 PSAY OemToAnsi(STR0025) //"Mes "
	@ 05,085 PSAY aMeses[_nMes]
	@ 05,095 PSAY OemToAnsi(STR0026) //"Ano"
	@ 05,100 PSAY StrZero(Year(dDtFim),4)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cabecalho para o Relatorio.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 07,000 PSAY OemToAnsi(STR0037) //"   Factura    Fecha                  CLIENTE                    RUT              Ventas                         VENTAS AFECTAS                  "   
@ 08,000 PSAY OemToAnsi(STR0038) //"                                                                                 Exentas            Valor Neto      IVA           Total         "   
nLin := 10
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020RodF  ºAutor  ³Jose Lucas          º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rodape do Libro de Faturas de IVA ( Ventas ).              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020RodF
Static Function R020RodF()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Dispara a funcao para impressao do Rodape.   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nLin := nLin + 1

If  !(lFinRel)
    R020RdpF()	
	R020CabF()                                         
	@ nLin,000 PSAY OemToAnsi(STR0028) //"Transporte :"
	nLin:= nlin+1
EndIf
lFinRel := .F.

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³R020RdpF  ºAutor  ³Jose Lucas		     º Data ³  06/07/98   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rodape do Libro de Faturas de IVA ( Ventas ).              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LIBRBOL		                                              º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
// Substituido pelo assistente de conversao do AP5 IDE em 09/09/99 ==> Function R020RdpF
Static Function R020RdpF()

nLin := nLin + 1

@ nLin,065 PSAY OemToAnsi(STR0039)  //"T O T A L E S : "         
@ nLin,082 PSAY nTotIsento PICTURE cPictVals //"@E( 999,999,999"
@ nLin,104 PSAY nBasImpFat PICTURE cPictVals //"@E( 999,999,999"
@ nLin,120 PSAY nValImpFat PICTURE cPictVals //"@E( 999,999,999"
@ nLin,136 PSAY nTotalFat  PICTURE cPictVals //"@E( 999,999,999"

Return

